
# ```move```进阶:```move```实现```ERC20```标准代币

## 1.创建一个工程

+ 第一种：使用IDEA创建一个move工程，项目名```FirstToken```
+ 第二种：手动创建项目
  + 创建项目文件夹 ```mkdir FirstToken  ```
  + 切换到当前的工程目录 ```cd FirstToken```
  + 创建合约目录目录 ``mkdir sources```
  + 创建项目配置文件 ```touch Move.toml```
  + idea导入```FirstToken````工程

## 2. 在工程目录底下执行```aptos init````
> 执行完之后，工程里面会多出一个```.aptos```的目录。里面有一个```config.yaml```,里面配置的如下：
```yaml
---
profiles:
  default:
    private_key: "****"
    public_key: "****"
    account: dc33b8ca18676247c66e607bc638b54b6cbc789d68cb20e5158ce8fe6131434b
    rest_url: "https://fullnode.testnet.aptoslabs.com"
```


> ```config.yaml```包含了刚刚配置的私钥、公钥、账户和对应网络的```RPC```节点。然后编辑```Move.toml```（注:通过IDEA创建的项目，Move.toml已经引入了相关依赖）
```toml
[package]
name = 'FirstToken'
version = '1.0.0'
[dependencies.AptosFramework]
git = 'https://github.com/aptos-labs/aptos-core.git'
rev = 'main'
subdir = 'aptos-move/framework/aptos-framework'

[addresses]
sender='<config.yaml profiles.default.account>'
```

> 最终的项目结构如下图:
![img](../asset/first_token.png)

## 3.在sources目录下创建一个```FirstToken.move```的```module```，开始编写Erc20合约
```move
module sender::FirstToken {
    use std::signer;
    use std::string;
    use aptos_framework::account;
    use aptos_framework::event;

    struct DepositEvent has drop, store {
        amount: u128,
    }

    struct WithdrawEvent has drop, store {
        amount: u128,
    }

    struct Coin<phantom CoinType> has store {
        value: u128
    }

    struct CoinStore<phantom CoinType> has key {
        coin: Coin<CoinType>,
        deposit_events: event::EventHandle<DepositEvent>,
        withdraw_events: event::EventHandle<WithdrawEvent>,
    }

    struct CoinInfo<phantom CoinType> has key {
        name: string::String,
        symbol: string::String,
        decimals: u8,
        supply: u128,
        cap: u128,
    }


    const OWNER: address = @sender;

    const REGISTERED: u64 = 1;

    const INVALID_OWNER: u64 = 2;

    const THE_ACCOUNT_IS_NOT_REGISTERED: u64 = 3;

    const INSUFFICIENT_BALANCE: u64 = 4;

    const ECOIN_INFO_ALREADY_INIT: u64 = 5;

    const EXCEEDING_THE_TOTAL_SUPPLY: u64 = 6;

    public entry fun initialize<CoinType>(
        address: &signer,
        name: string::String,
        symbol: string::String,
        decimals: u8,
        supply: u128
    ) {
        assert!(signer::address_of(address) == OWNER, INVALID_OWNER);
        assert!(!exists<CoinInfo<CoinType>>(OWNER), ECOIN_INFO_ALREADY_INIT);
        move_to(address, CoinInfo<CoinType> {  name, symbol, decimals, supply, cap: 0 });
    }

    fun deposit<CoinType>(account_addr: address, coin: Coin<CoinType>) acquires CoinStore {
        assert!(exists_coin<CoinType>(account_addr), THE_ACCOUNT_IS_NOT_REGISTERED);
        let balance = getBalance<CoinType>(account_addr);
        let coin_store = borrow_global_mut<CoinStore<CoinType>>(account_addr);
        let balance_ref = &mut coin_store.coin.value;
        *balance_ref = balance + coin.value;
        event::emit_event(&mut coin_store.withdraw_events,WithdrawEvent { amount: coin.value });
        let Coin { value: _ } = coin;
    }


    fun withdraw<CoinType>(account_addr: address, amount: u128): Coin<CoinType> acquires CoinStore {
        assert!(exists_coin<CoinType>(account_addr), THE_ACCOUNT_IS_NOT_REGISTERED);
        let balance = getBalance<CoinType>(account_addr);
        assert!(balance >= amount, INSUFFICIENT_BALANCE);
        let coin_store = borrow_global_mut<CoinStore<CoinType>>(account_addr);
        let balance_ref = &mut coin_store.coin.value;
        *balance_ref = balance - amount;
        event::emit_event(&mut coin_store.withdraw_events,WithdrawEvent {amount});
        Coin<CoinType> { value: amount }
    }

    public entry fun transfer<CoinType>(from: &signer, to: address, amount: u128) acquires CoinStore {
        let coin = withdraw<CoinType>(signer::address_of(from), amount);
        deposit<CoinType>(to, coin);
    }


    public entry fun burn<CoinType>(owner: &signer, amount: u128) acquires CoinStore, CoinInfo {
        assert!(signer::address_of(owner) == OWNER, INVALID_OWNER);
        let coin = withdraw<CoinType>(signer::address_of(owner), amount);
        let Coin<CoinType> { value: amount } = coin;
        let cap = &mut borrow_global_mut<CoinInfo<CoinType>>(OWNER).cap;
        *cap = *cap - amount;
        let supply = &mut borrow_global_mut<CoinInfo<CoinType>>(OWNER).supply;
        *supply = *supply - amount;
    }

    public entry fun mint<CoinType>(owner: &signer, to: address, amount: u128) acquires CoinStore, CoinInfo {
        assert!(signer::address_of(owner) == OWNER, INVALID_OWNER);
        assert!(
            borrow_global<CoinInfo<CoinType>>(OWNER).cap + amount <= borrow_global<CoinInfo<CoinType>>(OWNER).supply,
            EXCEEDING_THE_TOTAL_SUPPLY
        );
        deposit<CoinType>(to, Coin { value: amount });
        let cap = &mut borrow_global_mut<CoinInfo<CoinType>>(OWNER).cap;
        *cap = *cap + amount;
    }


    public fun getBalance<CoinType>(owner: address): u128 acquires CoinStore {
        assert!(exists_coin<CoinType>(owner), THE_ACCOUNT_IS_NOT_REGISTERED);
        borrow_global<CoinStore<CoinType>>(owner).coin.value
    }

    public entry fun register<CoinType>(address: &signer) {
        let account = signer::address_of(address);
        assert!(!exists<CoinStore<CoinType>>(account), REGISTERED);
        move_to(address, CoinStore<CoinType> {
            coin: Coin { value: 0 },
            deposit_events: account::new_event_handle<DepositEvent>(address),
            withdraw_events: account::new_event_handle<WithdrawEvent>(address)
        });
    }

    public fun exists_coin<CoinType>(account_addr: address): bool {
        exists<CoinStore<CoinType>>(account_addr)
    }
}
```

## 4.编译并发布合约到测试网
### 4.1 编译测试合约代码
```shell
 aptos move test   --named-addresses sender=0xdc33b8ca18676247c66e607bc638b54b6cbc789d68cb20e5158ce8fe6131434b
```
### 4.2 发布合约到测试网
```shell
 aptos move publish   --named-addresses sender=0xdc33b8ca18676247c66e607bc638b54b6cbc789d68cb20e5158ce8fe6131434b
```
> 输出结果如下，说明部署成功
```json
{
  "Result": {
    "transaction_hash": "0xa0948a75d39ead700832c524fdff7e8ebd30204828827bf273ca159a1d442b36",
    "gas_used": 1992,
    "gas_unit_price": 100,
    "sender": "dc33b8ca18676247c66e607bc638b54b6cbc789d68cb20e5158ce8fe6131434b",
    "sequence_number": 11,
    "success": true,
    "timestamp_us": 1667743324550864,
    "version": 334597799,
    "vm_status": "Executed successfully"
  }
}

```

### 4.3 去区块链浏览器查看
>在```resource```下多了```FirstToken```这个资源

![image](../asset/first_token_explorer.png)

### 4.4 与合约交互
```shell
aptos move run --function-id 0xdc33b8ca18676247c66e607bc638b54b6cbc789d68cb20e5158ce8fe6131434b::FirstToken::initialize --args string:eth string:eth u8:8 u128:100000000
```

----
> 至此关于aptos-cli的常用的命令已经介绍，详细使用请浏览[官方教程](https://aptos.dev/cli-tools/aptos-cli-tool/use-aptos-cli/)。
> 这一节的重点主要是使用```move```语言编写智能合约的如何通过aptos-cli去编译，发布，运行和测试。










